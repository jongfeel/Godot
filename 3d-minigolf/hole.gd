extends Node3D

enum { AIM, SET_POWER, SHOOT, WIN }

@export var power_speed = 100
@export var angle_speed = 1.1

var angle_change = 1
var arrow_base_angle = 0.0
var power = 0
var power_change = 1
var shots = 0
var state = AIM

# Called when the node enters the scene tree for the first time.
func _ready() -> void:
	$Arrow.hide()
	$Ball.position = $Tee.position
	change_state(AIM)
	$UI.show_message("Get Ready!")

func change_state(new_state: int) -> void:
	state = new_state
	match state:
		AIM:
			$Arrow.position = $Ball.position
			$Arrow.rotation.y = arrow_base_angle
			$Arrow.show()
		SET_POWER:
			power = 0
		SHOOT:
			$Arrow.hide()
			$Ball.shoot($Arrow.rotation.y, power / 15)
			shots += 1
			$UI.update_shots(shots)
		WIN:
			$Ball.hide()
			$Arrow.hide()
			$UI.show_message("Win!")

func _input(event: InputEvent) -> void:
	if event.is_action_pressed("click"):
		match state:
			AIM:
				change_state(SET_POWER)
			SET_POWER:
				change_state(SHOOT)

# Called every frame. 'delta' is the elapsed time since the previous frame.
func _process(delta: float) -> void:
	match state:
		AIM:
			animate_arrow(delta)
		SET_POWER:
			animate_power(delta)
		SHOOT:
			pass

func animate_arrow(delta: float) -> void:
	$Arrow.rotation.y += angle_speed * angle_change * delta
	if $Arrow.rotation.y > arrow_base_angle + PI / 2:
		angle_change = -1
	if $Arrow.rotation.y < arrow_base_angle - PI / 2:
		angle_change = 1
		
func animate_power(delta: float) -> void:
	power += power_speed * power_change * delta
	if power >= 100:
		power_change = -1
	if power <= 0:
		power_change = 1
	$UI.update_power_bar(power)


func _on_hole_body_entered(body: Node3D) -> void:
	if body.name == "Ball":
		print("win!")
		change_state(WIN)


func _on_ball_stopped(angle: float) -> void:
	if state == SHOOT:
		arrow_base_angle = angle
		change_state(AIM)
